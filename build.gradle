group 'net.lenords'
version '0.0.1-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    // https://mvnrepository.com/artifact/javax.servlet/javax.servlet-api
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.0'

    //for serialization
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

    //for ssh tunneling
    compile 'com.jcraft:jsch:0.1.54'
    compile 'org.json:json:20171018'
    //Mysql Connector:
    compile 'mysql:mysql-connector-java:5.1.45'

    //test dependencies
    testCompile('org.junit.jupiter:junit-jupiter-api:5.0.1')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.0.1')
}

task createJar(type: Jar) {
    baseName = 'reporter'
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

task copyJarToTomcat(type: Copy) {
    from createJar //copy build into tomcat server folder
    into "/Library/Tomcat/webapps/repley/WEB-INF/lib"
}

task copyWebToTomcat(type: Copy) {
    from "web/"
    into "/Library/Tomcat/webapps/repley"
}

task copyConfToTomcat(type: Copy) {
    from "conf/"
    into "/Library/Tomcat/webapps/repley/WEB-INF/lib/conf"
}

task stopTomcat(type: Exec) {
    workingDir '/Library/Tomcat/bin'
    commandLine './shutdown.sh'
}
task startTomcat(type: Exec) {
    workingDir '/Library/Tomcat/bin'
    commandLine './startup.sh'
}

//build servlet: update the jar in tomcat
task buildlet (dependsOn: [stopTomcat, createJar, copyWebToTomcat, copyConfToTomcat, copyJarToTomcat, startTomcat])